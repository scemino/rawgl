if (FIPS_WINDOWS)
    add_definitions(-D_CRT_SECURE_NO_WARNINGS)
endif()

# the general sokol implementations library (compiled as C, C++ or ObjC depending on platform)
fips_begin_lib(sokol)
    fips_files(clock.c clock.h fs.c fs.h gfx.c gfx.h)
    sokol_shader(shaders.glsl ${slang})
    if (FIPS_OSX)
        fips_files(sokol.m)
        fips_frameworks_osx(Foundation)
        if (FIPS_IOS)
            fips_frameworks_osx(UIKit AudioToolbox AVFoundation)
            if (SOKOL_USE_METAL)
                fips_frameworks_osx(Metal MetalKit)
            else()
                fips_frameworks_osx(OpenGLES GLKit)
            endif()
        else()
            if (SOKOL_USE_METAL)
                fips_frameworks_osx(Cocoa QuartzCore Metal MetalKit AudioToolbox)
            else()
                fips_frameworks_osx(Cocoa QuartzCore OpenGL AudioToolbox)
            endif()
        endif()
    else()
        fips_files(sokol.c)
        if (FIPS_ANDROID)
            fips_libs(GLESv3 EGL log android)
            if (SOKOL_USE_OPENSLES)
                fips_libs(OpenSLES)
            else()
                fips_libs(aaudio)
            endif()
        elseif (FIPS_LINUX)
            if (SOKOL_USE_EGL_GLES3 OR SOKOL_USE_EGL_GL)
                fips_libs(X11 Xi Xcursor EGL GL m dl asound)
            else()
                fips_libs(X11 Xi Xcursor GL m dl asound)
            endif()
        endif()
    endif()
fips_end_lib()

# optional UI library (using Dear ImGui)
fips_begin_lib(ui)
    fips_files(ui.cc ui.h)
    fips_deps(imgui)
fips_end_lib()

# optional miniz library
fips_begin_lib(miniz)
    fips_files(miniz.c miniz.h)
fips_end_lib()
